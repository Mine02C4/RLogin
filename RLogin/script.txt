
////////////////////////////////////////////////////
// Syntax

echo(base64e("kmiya:ken"));
    
echo("Syntax ");

if ( 0 ) exit(1); else a = 1;
if ( a == 0 ) { exit(1); } else if ( a == "" ) { exit(1); }
echo("if ");

if ( 0 || 1 ) { if ( 1 || 0 ) echo("|| "); }
if ( 0 && 1 ) exit(1);
if ( 1 && 0 ) exit(1);
if ( 1 && 1 ) echo("&& ");

if ( 0 > 1 ) exit(1);
if ( 1 > 1 ) exit(1);
if ( 1 < 0 ) exit(1);
if ( 1 < 1 ) exit(1);
if ( 0 >= 1 ) exit(1);
if ( 1 >= 2 ) exit(1);
if ( 1 <= 0 ) exit(1);
if ( 2 <= 1 ) exit(1);
echo("<><=>= ");

a = 0;
while ( a < 10 ) { if ( ++a == 5 ) break; }
if ( a != 5 ) exit(1);
echo("while ");

for ( a = 0 ; a < 10 ; a++ );
if ( a != 10 ) exit(1);
for ( a = 0 ; a <= 10 ; a++ );
if ( a != 11 ) exit(1);
for ( a = 10 ; a > 0 ; a-- );
if ( a != 0 ) exit(1);
for ( a = 10 ; a >= 1 ; a-- );
if ( a != 0 ) exit(1);
echo("for ");

a = 0;
do { a++; } while ( a < 10 );
if ( a != 10 ) exit(1);
echo("do ");

a = 0;
switch(a) {
case 1: exit(1); break;
case 0: echo("switch "); break;
}

y = 20;
z = 10;
function aaa(x, y, z) { a = x; y = 100; w = z++;
if ( z != 11 ) return 0; else return 999; }
if ( aaa(222, 0, z) != 999 || a != 222 || y != 20 || z != 10 || w != 0 || aaa.w != 10 ) exit(1);
bbb = aaa;
if ( bbb(111, 0, z) != 999 || a != 111 || y != 20 || z != 10 || w != 0 || bbb.w != 10 ) exit(1);
echo("function ");

a = 1;
a[0] = 10;
a[0][0] = 100;
a[1] = 20;
a[0][1] = 200;
a[] = 3;
a[0][] = 5;
if ( a != 1 || a[0] != 10 || a[1] != 20 || a[2] != 3 ) exit(1);
if ( a[0][0] != 100 || a[0][1] != 200 || a[0][2] != 5 ) exit(1);
reset(a);
echo("[1] ");
a = [1, 2:2, 1:3];
b = a;
if ( a[0] != 1 || a[1] != 3 || a[2] != 2 ) exit(1);
if ( b[0] != 1 || b[1] != 3 || b[2] != 2 ) exit(1);
reset(a);
reset(b);
echo("[2] ");

a = "a";
a.b = "ab";
a.b.c = "abc";
if ( a != "a" || a.b != "ab" || a.b.c != "abc" ) exit(1);
reset(a);
echo(". ");

a = "ABC";
if ( a{1} != 'B' ) exit(1);
a{1} = 'Z';
a{} = 'X';
a{} = 'Y';
a{} = 'Z';
if ( a != "AZCXYZ" ) exit(1);
reset(a);
echo("{} ");

a = 3;
b = 4;
if ( (a * 2) != 6 || (a * b * 2) != 24 ) exit(1);
if ( (a / 2) != 1 || (b / a) != 1 ) exit(1);
if ( (a % 2) != 1 || (b % a) != 1 ) exit(1);
if ( (a + 2) != 5 || (a + b + 2) != 9 ) exit(1);
if ( (a - 2) != 1 || (b - a - 1) != 0 ) exit(1);

a = 3.0;
b = 6.0;
if ( abs((a * 2.0) - 6.0) > 0.0 || abs((a * b * 2) - 36.0) > 0.0 ) exit(1);
if ( abs((a / 2.0) - 1.5) > 0.0 || abs((b / a) - 2.0) > 0.0 ) exit(1);
if ( abs((a + 2.0) - 5.0) > 0.0 || abs((a + b + 2) - 11.0) > 0.0 ) exit(1);
if ( abs((a - 2.0) - 1.0) > 0.0 || abs((b - a - 1) - 2.0) > 0.0 ) exit(1);
echo("*/%%+- ");

a = 2 * 3 + 4 / 2;
if ( a != 8 ) exit(1);
a = 2 * (3 + 4) / 2;
if ( a != 7 ) exit(1);
echo("() ");

echo("OK\n");

////////////////////////////////////////////////////
// Func01

echo("Func01 ");

a = 10;		if ( a != 10 ) exit(1);
a = 10;		if ( int(a) != 10 ) exit(1);
a = 10.0;	if ( int(a) != 10 ) exit(1);
a = "10";	if ( int(a) != 10 ) exit(1);
a = L"10";	if ( int(a) != 10 ) exit(1);
a = "\x0A"; if ( int(a{0}) != 10 ) exit(1);
echo("int ");

a = 10;		if ( int64(a) != 10 ) exit(1);
a = 10.0;	if ( int64(a) != 10 ) exit(1);
a = "10";	if ( int64(a) != 10 ) exit(1);
a = L"10";	if ( int64(a) != 10 ) exit(1);
a = "\x0A"; if ( int64(a{0}) != 10 ) exit(1);
echo("int64 ");

a = 10;		if ( a != "10" ) exit(1);
a = 10;		if ( str(a) != "10" ) exit(1);
a = 10.0;	if ( str(a) != "10" ) exit(1);
a = "10";	if ( str(a) != "10" ) exit(1);
a = L"10";	if ( str(a) != "10" ) exit(1);
echo("str ");

a = 10;		if ( a != L"10" ) exit(1);
a = 10;		if ( wstr(a) != L"10" ) exit(1);
a = 10.0;	if ( wstr(a) != L"10" ) exit(1);
a = "10";	if ( wstr(a) != L"10" ) exit(1);
a = L"10";	if ( wstr(a) != L"10" ) exit(1);
echo("wstr ");

a = 10;		if ( length(a) != 2 ) exit(1);
a = 10.0;	if ( length(a) != 2 ) exit(1);
a = "10";	if ( length(a) != 2 ) exit(1);
a = L"10";	if ( length(a) != 2 ) exit(1);
echo("length ");

a["xyz"] = 900;
a["abc"] = 500;
a["def"] = 100;
sort(a);	if ( a[0] != 100 || a[1] != 500 || a[2] != 900 ) exit(1);
ksort(a);	if ( a[0] != 500 || a[1] != 100 || a[2] != 900 ) exit(1);
rsort(a);	if ( a[0] != 900 || a[1] != 500 || a[2] != 100 ) exit(1);
krsort(a);	if ( a[0] != 900 || a[1] != 100 || a[2] != 500 ) exit(1);
echo("sort ");

if ( count(a) != 3 ) exit(1);
echo("count ");

if ( key(a[0]) != "xyz" || key(a[1]) != "def" || key(a[2]) != "abc" ) exit(1);
echo("key ");

reset(a);
if ( count(a) != 0 || a != 0 ) exit(1);
echo("reset ");

echo("OK\n");

////////////////////////////////////////////////////
// Func02

echo("Func02 ");

a = "abcxyzdef";
if ( substr(a, 3, 3) != "xyz" ) exit(1);
echo("substr ");

if ( strstr(a, "xyz") != 3 ) exit(1);
echo("strstr ");

b = ereg("xyz", a);
if ( b == 0 || b[0] != "xyz" ) exit(1);
echo("ereg ");

b = replace("xyz", "XYZ", a);
if ( b != "abcXYZdef" ) exit(1);
echo("replace ");

a = "abc xyz   def";
b = split("[ ]+", a);
if ( b == 0 || b[0] != "abc" || b[1] != "xyz" || b[2] != "def" ) exit(1);
echo("split ");

a = sprintf("abc%sdef%dOPQ%d", "xyz", 100, int64(9999));
if ( a != "abcxyzdef100OPQ9999" ) exit(1);
echo("sprintf ");

a = time(); if ( a == 0 ) exit(1);
echo("time ");

a = mktime(0, 0, 0, 3, 12, 2008);
if ( a != 1205247600 ) exit(1);
echo("mktime ");

b = strftime("%m/%d/%y", a);
if ( b != "03/12/08" ) exit(1);
echo("strftime ");

b = getdate(a);
if ( b["year"] != 2008 || b["mon"] != 3 || b["mday"] != 12 ) exit(1);
echo("getdate ");

echo("OK\n");

////////////////////////////////////////////////////
// Func03

echo("Func03 ");

if ( (a = fopen("test.txt", "wb")) == 0 ) exit(1);
fputc(0x31, a);
fputs("abc\n", a);
fwrite("XYZ", a);
fclose(a);

if ( (a = fopen("test.txt", "rb")) == 0 ) exit(1);
if ( fgetc(a) != '1' ) exit(1);
if ( ftell(a) != 1 ) exit(1);
if ( fgets(a) != "abc\n" ) exit(1);
if ( fread(3, a) != "XYZ" ) exit(1);
if ( fseek(a, 1, SEEK_SET) != 0 ) exit(1);
if ( fgets(a) != "abc\n" ) exit(1);
fclose(a);
echo("fopen fputc fputs fwrite fgetc fgets fread ftell fseek fclose ");

a = file("test.txt");
if ( a[0] != "1abc\n" ) exit(1);
if ( a[1] != "XYZ" ) exit(1);
echo("file ");

a = stat("test.txt");
if ( a["size"] != 8 ) exit(1);
echo("stat ");
reset(a);

a = basename("C:\\XXX\\ABC.TXT");
if ( a != "ABC.TXT" ) exit(1);
echo("basename ");

a = dirname("C:\\XXX\\ABC.TXT");
if ( a != "C:\\XXX" ) exit(1);
echo("dirname ");

copy("test.txt", "test02.txt");
rename("test02.txt", "test03.txt");
a = stat("test03.txt");
if ( a["size"] != 8 ) exit(1);
echo("copy rename ");
reset(a);

delete("test.txt");
delete("test03.txt");
if ( stat("test.txt") != 0 ) exit(1);
if ( stat("test03.txt") != 0 ) exit(1);
echo("delete ");

if ( getcwd() == "" ) exit(1);
echo("getcwd ");

echo("OK\n");

////////////////////////////////////////////////////
// Func04

echo("Func04 ");

srand(0);
if ( rand() != 38 ) exit(1);
srand(100);
if ( rand() != 365 ) exit(1);
if ( rand() != 1216 ) exit(1);
echo("srand rand ");

if ( floor(10.01) != 10.0 ) exit(1);
echo("floor ");

if ( sin(PI / 2) != 1.0 ) exit(1);
echo("sin ");
if ( cos(PI) != -1.0 ) exit(1);
echo("cos ");
if ( sprintf("%g", tan(PI / 4)) != "1" ) exit(1);
echo("tan ");

if (abs(-1) != 1 ) exit(1);
if (abs(1.0) != 1.0 ) exit(1);
echo("abs ");

if ( pow(2, 3) != 8 ) exit(1);
echo("pow ");
 
if ( log(E) != 1 ) exit(1);
echo("log ");

echo("OK\n");

////////////////////////////////////////////////////
// Func05

echo("Func05 ");

a = iconv("SJIS", "EUC-JP", "Š¿Žš");
if ( a != "\xb4\xc1\xbb\xfa" ) exit(1);
echo("iconv ");

a = "abcdefg";
if ( md5(a) != "7ac66c0f148de9519b8bd264312c4d64" ) exit(1);
echo("md5 ");

if ( sha1(a) != "2fb5e13419fc89246865e7a324f476ec624e8740" ) exit(1);
echo("sha1 ");

if ( base64d(base64e(a)) !=  a ) exit(1);
echo("base64 ");

if ( quotedd(quotede(a)) !=  a ) exit(1);
echo("quoted ");

if ( base16d(base16e(a)) !=  a ) exit(1);
echo("base16 ");

echo("OK\n");

////////////////////////////////////////////////////
// Func08

echo("Func08 ");

a = regopen("XYZ"); s = wstr("ABCXYZEFG");
for ( n = 0 ; n < length(s) ; n++ ) if ( (r = regchar(s{n}, a)) ) x = r;
if ( x == 0 || x[0] != "XYZ" ) exit(1);
x = regstr("ABCDXYZ", a);
if ( x == 0 || x[0] != "XYZ" ) exit(1);
regclose(a);
echo("regopen regclose regchar regstr ");

echo("OK\n");

////////////////////////////////////////////////////
// Class

r = 20;
class test {
    var r;
    var i;
    function add(a, b) {
		var x = 9;
		r = a;
		i = b;
		return a + i;
    }
}
if ( test.add(2, 3) != 5 ) exit(1);
if ( r != 20 || test.r != 2 || test.i != 3 || test.add.x != 9 ) exit(1);
echo("var ");

echo("OK\n");

////////////////////////////////////////////////////

echo("Test Script End\n");
